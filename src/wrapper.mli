module type S = sig
  type enc
  type node
  type tree
  type trees

  module M : Rocq_monad_utils.S with type enc = enc and type tree = tree
  module Bindings : Bindings.S with type 'a mm = 'a M.mm

  module ConstructorBindings :
    Constructor_bindings.S
    with type 'a mm = 'a M.mm
     and type ind = M.Ind.t
     and type instructions = Bindings.Instructions.t
     and type bindings = Bindings.t
     and type constrmap = Bindings.ConstrMap.t'

  module Model :
    Model.S
    with type base = enc
     and type tree = tree
     and type trees = trees
     and type constructorbindings = ConstructorBindings.t

  module Decode :
    Decoder.S
    with type enc = enc
     and type state = Model.State.t
     and type states = Model.State.Set.t
     and type partition = Model.Partition.t
     and type label = Model.Label.t
     and type labels = Model.Label.Set.t
     and type note = Model.Note.t
     and type annotation = Model.Annotation.t
     and type annotations = Model.Annotation.Set.t
     and type transition = Model.Transition.t
     and type transitions = Model.Transition.Set.t
     and type action = Model.Action.t
     and type actions = Model.Action.Set.t
     and type actionmap = Model.Action.Map.t'
     and type edgemap = Model.EdgeMap.t'
     and type rocqlts = Model.Info.Meta.RocqLTS.t
     and type info = Model.Info.t
     and type lts = Model.LTS.t
     and type fsm = Model.FSM.t
     and type result = Model.Bisimilarity.Result.t
     and type bisimilarity = Model.Bisimilarity.t

  module Theory :
    Theories_enc.S
    with type enc = enc
     and type 'a mm = 'a M.mm
     and type 'a im = 'a M.mm

  module Weak : Weak.S with type enc = enc

  module Config :
    Config_loader.S with type weak = Weak.t and type 'a mm = 'a M.mm

  val result_log
    :  ?decode:bool
    -> (module Json.S with type k = 'a)
    -> (module Json.S with type k = 'a)
    -> (module Json.S with type k = 'a)

  val handle_results
    :  Output.Kind.t
    -> string
    -> 'a
    -> (module Json.S with type k = 'a)
    -> unit

  val extract_lts
    :  Libnames.qualid
    -> Constrexpr.constr_expr
    -> Libnames.qualid list
    -> Weak.t option
    -> Model.LTS.t M.mm

  (** [similarity r]: for a bisimilarity check's result [r], the greatest
      weak simulation from FSM a to FSM b among the pairs reachable from
      their start states ({!Model.Product.simulation}), or [None] if either
      FSM has no start state. When an FSM is saturated on demand, that walk
      saturates state after state, so it needs [MeBi Config Bounds Game <n>] and stays within it: without the bound, or past it, it is a user
      error saying how to allow it. *)
  val similarity : Model.Bisimilarity.t -> Model.Product.Pair.Set.t option

  module Command : sig
    val build_lts
      :  ?weak:Weak.t option
      -> Libnames.qualid
      -> Constrexpr.constr_expr
      -> Libnames.qualid list
      -> Model.LTS.t M.mm

    val build_fsm
      :  ?weak:Weak.t option
      -> Libnames.qualid
      -> Constrexpr.constr_expr
      -> Libnames.qualid list
      -> Model.FSM.t M.mm

    type t =
      | MakeLTS of rocq_args
      | MakeFSM of rocq_args
      | Saturate of rocq_args
      | Minimize of rocq_args
      | Merge of rocq_pair
      | CheckBisim of rocq_pair
      | CheckSim of rocq_pair
      | BenchmarkGraph of (rocq_args * (int * int))

    and rocq_args = Constrexpr.constr_expr * Libnames.qualid

    and rocq_pair =
      { a : rocq_args
      ; b : rocq_args
      }

    val do_make_lts
      :  Constrexpr.constr_expr * Libnames.qualid
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val do_make_fsm
      :  Constrexpr.constr_expr * Libnames.qualid
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val do_saturate
      :  Constrexpr.constr_expr * Libnames.qualid
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val do_minimize
      :  Constrexpr.constr_expr * Libnames.qualid
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val build_fsms
      :  rocq_args
      -> rocq_args
      -> Libnames.qualid list
      -> (Model.FSM.t * Model.FSM.t) M.mm

    val do_merge
      :  rocq_pair
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val do_check_bisim
      :  rocq_pair
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    (** [do_check_sim { a; b } refs]: decide whether [a]'s term is weakly
        simulated by [b]'s ([MeBi Run Sim]), reporting the verdict. Bisimilar
        states are similar outright; otherwise {!similarity} decides. Not
        similar is an error under [MeBi Config FailIf NotBisimilar True]
        (the default), else a warning. *)
    val do_check_sim
      :  rocq_pair
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    (** [do_benchmark_graph ((xs, primary_lts), (time, repeat)) refs] is always [None]. This command is similar to {!val:do_make_lts} except that it can handle a list of [xs] that each use the same [primary_lts] and [refs]. {i {b Note:} We use {!Rocq_utils.extract_benchmark_args} to obtain the list of xs, as [g_mebi] only knows it to be a [constr] (i.e., a [Constrexpr.constr_expr]).} {b Param [time]} is the {i minimum} run time per iteration and {b Param [repeat]} is the number of times to repeat each of the benchmarks. {i See {!Benchmarking}.}
    *)
    val do_benchmark_graph
      :  rocq_args * (int * int)
      -> Libnames.qualid list
      -> Decode.bisimilarity option M.mm

    (** [run refs x] is the entrypoint of {!Command}. {b Param [refs]} is a list of {b Rocq} inductive-LTS that may be used for the upper layers of a {i multi-layered} LTS. {b Param [x]} is a {!t} that specifies the command to be run.
    *)
    val run : Libnames.qualid list -> t -> Model.Bisimilarity.t option M.mm
  end
end

module Make (Enc : Encoding.S) :
  S
  with type enc = Enc.t
   and type node = Enc.Tree.Node.t
   and type tree = Enc.Tree.t
   and type trees = Enc.Trees.t

val make : ?enc:(unit -> (module Encoding.S)) -> unit -> (module S)

(** The shared instance the [MeBi ...] vernaculars run against, created on first
    use. Commands no longer build their own: per-command state lifetime is
    carried by the [~reset_encoding:true] they already pass, not by rebuilding
    the module tree. *)
val get : unit -> (module S)

(** Drops the shared instance so the next [get] builds a fresh one. *)
val reset : unit -> unit
