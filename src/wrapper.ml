module type S = sig
  type enc
  type node
  type tree
  type trees

  module M : Rocq_monad_utils.S with type enc = enc and type tree = tree
  module Bindings : Bindings.S with type 'a mm = 'a M.mm

  module ConstructorBindings :
    Constructor_bindings.S
    with type 'a mm = 'a M.mm
     and type ind = M.Ind.t
     and type instructions = Bindings.Instructions.t
     and type bindings = Bindings.t
     and type constrmap = Bindings.ConstrMap.t'

  module Model :
    Model.S
    with type base = enc
     and type tree = tree
     and type trees = trees
     and type constructorbindings = ConstructorBindings.t

  module Decode :
    Decoder.S
    with type enc = enc
     and type state = Model.State.t
     and type states = Model.State.Set.t
     and type partition = Model.Partition.t
     and type label = Model.Label.t
     and type labels = Model.Label.Set.t
     and type note = Model.Note.t
     and type annotation = Model.Annotation.t
     and type annotations = Model.Annotation.Set.t
     and type transition = Model.Transition.t
     and type transitions = Model.Transition.Set.t
     and type action = Model.Action.t
     and type actions = Model.Action.Set.t
     and type actionmap = Model.Action.Map.t'
     and type edgemap = Model.EdgeMap.t'
     and type rocqlts = Model.Info.Meta.RocqLTS.t
     and type info = Model.Info.t
     and type lts = Model.LTS.t
     and type fsm = Model.FSM.t
     and type result = Model.Bisimilarity.Result.t
     and type bisimilarity = Model.Bisimilarity.t

  module Theory :
    Theories_enc.S
    with type enc = enc
     and type 'a mm = 'a M.mm
     and type 'a im = 'a M.mm

  module Weak : Weak.S with type enc = enc

  module Config :
    Config_loader.S with type weak = Weak.t and type 'a mm = 'a M.mm

  val result_log
    :  ?decode:bool
    -> (module Json.S with type k = 'a)
    -> (module Json.S with type k = 'a)
    -> (module Json.S with type k = 'a)

  val handle_results
    :  Output.Kind.t
    -> string
    -> 'a
    -> (module Json.S with type k = 'a)
    -> unit

  val extract_lts
    :  Libnames.qualid
    -> Constrexpr.constr_expr
    -> Libnames.qualid list
    -> Weak.t option
    -> Model.LTS.t M.mm

  module Command : sig
    val build_lts
      :  ?weak:Weak.t option
      -> Libnames.qualid
      -> Constrexpr.constr_expr
      -> Libnames.qualid list
      -> Model.LTS.t M.mm

    val build_fsm
      :  ?weak:Weak.t option
      -> Libnames.qualid
      -> Constrexpr.constr_expr
      -> Libnames.qualid list
      -> Model.FSM.t M.mm

    type t =
      | MakeLTS of rocq_args
      | MakeFSM of rocq_args
      | Saturate of rocq_args
      | Minimize of rocq_args
      | Merge of rocq_pair
      | CheckBisim of rocq_pair
      | BenchmarkGraph of (rocq_args * (int * int))

    and rocq_args = Constrexpr.constr_expr * Libnames.qualid

    and rocq_pair =
      { a : rocq_args
      ; b : rocq_args
      }

    val do_make_lts
      :  Constrexpr.constr_expr * Libnames.qualid
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val do_make_fsm
      :  Constrexpr.constr_expr * Libnames.qualid
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val do_saturate
      :  Constrexpr.constr_expr * Libnames.qualid
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val do_minimize
      :  Constrexpr.constr_expr * Libnames.qualid
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val build_fsms
      :  rocq_args
      -> rocq_args
      -> Libnames.qualid list
      -> (Model.FSM.t * Model.FSM.t) M.mm

    val do_merge
      :  rocq_pair
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val do_check_bisim
      :  rocq_pair
      -> Libnames.qualid list
      -> Model.Bisimilarity.t option M.mm

    val do_benchmark_graph
      :  rocq_args * (int * int)
      -> Libnames.qualid list
      -> Decode.bisimilarity option M.mm

    val run : Libnames.qualid list -> t -> Model.Bisimilarity.t option M.mm
  end
end

module Make (Enc : Encoding.S) :
  S
  with type enc = Enc.t
   and type node = Enc.Tree.Node.t
   and type tree = Enc.Tree.t
   and type trees = Enc.Trees.t = struct
  type enc = Enc.t
  type node = Enc.Tree.Node.t
  type tree = Enc.Tree.t
  type trees = Enc.Trees.t

  module Benchmarking = Benchmarking.Make

  (* NOTE: to stop message spam when debugging, wrap the noisy call in
     [Logger.quiet (fun () -> ...)]. This used to require re-instantiating the
     whole module tree with a differently-configured logger (Logger.ReMake),
     which is why the attempt that lived here was left commented out. *)

  (** [module M] ... *)
  module M = Rocq_monad_utils.Make (Enc)

  (** [module Bindings] ... *)
  module Bindings = Bindings.Make (M)

  (** [module ConstructorBindings] ... *)
  module ConstructorBindings = Constructor_bindings.Make (M) (Bindings)

  (** [module Model] ... *)
  module Model = Model.Make (Enc) (ConstructorBindings)

  module LTS = Model.LTS
  module FSM = Model.FSM

  (** [module Decode] handles obtaining [EConstr.t] from [module M]. *)
  module Decode = Decoder.Make (Enc) (M) (ConstructorBindings) (Model)

  (** [module Theory] ... *)
  module Theory = Theories_enc.Make (Enc) (M) (M) (Theories.Make (Enc) (M))

  (** [module Weak] ... *)
  module Weak = Weak.Make (Enc) (M)

  module Config = Config_loader.Make (Enc) (M) (Weak)

  let result_log
        ?(decode : bool = true)
        (type a)
        (module FEnc : Json.S with type k = a)
        (module FDec : Json.S with type k = a)
    : (module Json.S with type k = a)
    =
    let module E : Json.S with type k = a =
      (val if decode && !Api.the_output_config.decode_results
           then (module FDec : Json.S with type k = a)
           else (module FEnc : Json.S with type k = a))
    in
    (module E : Json.S with type k = a)
  ;;

  let handle_results
        (type a)
        (m : Output.Kind.t)
        (s : string)
        (x : a)
        (module FLog : Json.S with type k = a)
    : unit
    =
    FLog.log ~m ~s x;
    match m with
    | Result -> if !Api.the_output_config.dump_results then FLog.write s x
    | _ -> ()
  ;;

  let check_if_lts_fail (x : LTS.t) : unit =
    if
      !Api.the_fail_flags.empty
      && (Int.equal (Model.State.Set.cardinal x.states) 1
          || Model.State.Set.is_empty x.states)
      && Model.Transition.Set.is_empty x.transitions
    then (
      Logger.trace ~__FUNCTION__ "LTS Empty";
      M.Err.lts_empty ())
    else if !Api.the_fail_flags.incomplete
    then (
      match x with
      | { info = { meta = Some { is_complete = false; bounds; _ }; _ }; _ } ->
        result_log (module Model.LTS) (module Decode.LTS)
        |> handle_results Result "LTS Incomplete" x;
        let rec bound : Model.Info.Meta.Bounds.t -> string = function
          | States n -> Printf.sprintf "%i states" n
          | Transitions n -> Printf.sprintf "%i transitions" n
          | Merged (a, b) -> Printf.sprintf "%s and %s" (bound a) (bound b)
        in
        M.Err.lts_incomplete
          (Printf.sprintf
             "exploration stopped at the bound of %s, with %i states and %i \
              transitions found and more still unexplored. Raise the bound \
              with [MeBi Config Bounds As Num States <n>] (or [... Num \
              Transitions <n>]), or accept a partial LTS with [MeBi Config \
              FailIf Incomplete False]. A large LTS can still be too big to \
              saturate."
             (bound bounds)
             (Model.State.Set.cardinal x.states)
             (Model.Transition.Set.cardinal x.transitions))
      | _ -> ())
    else ()
  ;;

  (** Guards every saturation: computes, without saturating, how many weak
      actions saturating [x] would produce ({!Model.SaturationEstimate}), and
      refuses past [Api.the_saturation_bound] -- or, with [MeBi Config FailIf Oversaturated False], warns and carries on. Saturating [Proc/Test4]
      (74.6M weak actions) exhausted a 15GB machine; this says so up front.
      A no-op for an FSM with no silent labels, which saturation leaves
      unchanged. *)
  let check_saturation_size (name : string) (x : FSM.t) : unit =
    if Model.FSM.is_weak_mode x
    then (
      let e : Model.SaturationEstimate.t = Model.SaturationEstimate.fsm x in
      Logger.info
        (Printf.sprintf
           "Saturating %s: %s."
           name
           (Model.SaturationEstimate.to_string e));
      let bound : int = !Api.the_saturation_bound in
      let lo, hi = Api.bytes_per_weak_action in
      if e.weak > bound
      then (
        let msg : string =
          Printf.sprintf
            "saturating %s would produce %s, above the bound of %i. That needs \
             about %s--%s of memory (measured %i--%i bytes per weak action), \
             and can take a long time. Raise the bound with [MeBi Config \
             Bounds Saturation <n>] if your machine has the memory, or carry \
             on with only a warning with [MeBi Config FailIf Oversaturated \
             False]."
            name
            (Model.SaturationEstimate.to_string e)
            bound
            (Api.human_bytes (e.weak * lo))
            (Api.human_bytes (e.weak * hi))
            lo
            hi
        in
        if !Api.the_fail_flags.oversaturated
        then M.Err.saturation_too_large msg
        else Logger.warning (String.capitalize_ascii msg)))
  ;;

  let make_graph_args ()
    : (module Graph_type.Args with type enc = Enc.t and type tree = Enc.Tree.t)
    =
    (module struct
      type enc = Enc.t
      type tree = Enc.Tree.t

      module T : Hashtbl.S with type key = Enc.t = (val M.make_enc_hashtbl ())
      module S : Set.S with type elt = Enc.t = (val M.make_enc_set ())

      module D : Set.S with type elt = Enc.t * Enc.Tree.t =
        (val M.make_state_tree_pair_set ())

      let bounds : Api.bounds_args = !Config.the_bounds_args
    end : Graph_type.Args
      with type enc = Enc.t
       and type tree = Enc.Tree.t)
  ;;

  module G
      (X : Graph_type.Args with type enc = Enc.t and type tree = Enc.Tree.t) =
    Graph.Make (Enc) (M) (Weak) (Theory) (ConstructorBindings) (Model) (X)

  let extract_lts
        (primary_lts : Libnames.qualid)
        (init : Constrexpr.constr_expr)
        (names : Libnames.qualid list)
        (weak : Weak.t option)
    : LTS.t M.mm
    =
    Logger.trace __FUNCTION__;
    let module G = G ((val make_graph_args ())) in
    let grefs = Rocq_utils.libnames_to_globrefs (primary_lts :: names) in
    let open M.Syntax in
    let* the_graph : G.t = G.build ~weak init primary_lts grefs in
    let* the_lts : Model.LTS.t = G.extract the_graph in
    check_if_lts_fail the_lts;
    M.return the_lts
  ;;

  module Command = struct
    let build_lts
          ?(weak : Weak.t option = None)
          (primary_lts : Libnames.qualid)
          (init : Constrexpr.constr_expr)
          (names : Libnames.qualid list)
      : LTS.t M.mm
      =
      Logger.trace __FUNCTION__;
      Config.get_weak weak |> extract_lts primary_lts init names
    ;;

    let build_fsm
          ?(weak : Weak.t option = None)
          (primary_lts : Libnames.qualid)
          (init : Constrexpr.constr_expr)
          (names : Libnames.qualid list)
      : FSM.t M.mm
      =
      Logger.trace __FUNCTION__;
      let open M.Syntax in
      let* the_lts = build_lts ~weak primary_lts init names in
      Model.FSM.of_lts the_lts |> M.return
    ;;

    let do_make_lts (x, primary_lts) refs : Model.Bisimilarity.t option M.mm =
      Logger.trace __FUNCTION__;
      let open M.Syntax in
      Logger.info "Extracting LTS...";
      let* the_lts = build_lts primary_lts x refs in
      result_log (module Model.LTS) (module Decode.LTS)
      |> handle_results Result "Finished Extracting LTS" the_lts;
      M.return None
    ;;

    let do_make_fsm (x, primary_lts) refs : Model.Bisimilarity.t option M.mm =
      Logger.trace __FUNCTION__;
      Logger.info "Making FSM (from extracted LTS)...";
      let open M.Syntax in
      let* the_fsm = build_fsm primary_lts x refs in
      result_log (module Model.FSM) (module Decode.FSM)
      |> handle_results Result "Finished Making FSM" the_fsm;
      M.return None
    ;;

    let do_saturate (x, primary_lts) refs : Model.Bisimilarity.t option M.mm =
      Logger.trace __FUNCTION__;
      Logger.info "Making FSM (from extracted LTS)...";
      let open M.Syntax in
      let* the_fsm = build_fsm primary_lts x refs in
      result_log (module Model.FSM) (module Decode.FSM)
      |> handle_results Info "Finished Making FSM" the_fsm;
      check_saturation_size "the FSM" the_fsm;
      Logger.info "Saturating FSM...";
      let the_fsm = Model.FSM.saturate the_fsm in
      result_log (module Model.FSM) (module Decode.FSM)
      |> handle_results Result "Finished Saturating FSM" the_fsm;
      M.return None
    ;;

    let do_minimize (x, primary_lts) refs : Model.Bisimilarity.t option M.mm =
      Logger.trace __FUNCTION__;
      Logger.info "Making FSM (from extracted LTS)...";
      let open M.Syntax in
      let* the_fsm = build_fsm primary_lts x refs in
      result_log (module Model.FSM) (module Decode.FSM)
      |> handle_results Info "Finished Making FSM" the_fsm;
      check_saturation_size "the FSM" the_fsm;
      Logger.info "Minimizing FSM...";
      let { fsm; pi } : Model.Minimization.t = Model.Minimization.fsm the_fsm in
      Decode.Partition.log ~m:Info ~s:"pi" pi;
      result_log (module Model.FSM) (module Decode.FSM)
      |> handle_results Result "Finished Minimizing FSM" the_fsm;
      M.return None
    ;;

    type t =
      | MakeLTS of rocq_args
      | MakeFSM of rocq_args
      | Saturate of rocq_args
      | Minimize of rocq_args
      | Merge of rocq_pair
      | CheckBisim of rocq_pair
      | BenchmarkGraph of (rocq_args * (int * int))

    and rocq_args = Constrexpr.constr_expr * Libnames.qualid

    and rocq_pair =
      { a : rocq_args
      ; b : rocq_args
      }

    let build_fsms
          ((ax, alts) : rocq_args)
          ((bx, blts) : rocq_args)
          (refs : Libnames.qualid list)
      : (FSM.t * FSM.t) M.mm
      =
      Logger.trace __FUNCTION__;
      Logger.info "Making FSMs...";
      let open M.Syntax in
      Logger.info "Making FSM A...";
      let weak1 : Weak.t option = Config.get_the_weak_arg1 () in
      let* the_fsm_a = build_fsm ~weak:weak1 alts ax refs in
      result_log (module Model.FSM) (module Decode.FSM)
      |> handle_results Info "Finished Making FSM A" the_fsm_a;
      Logger.info "Making FSM B...";
      let weak2 : Weak.t option = Config.get_the_weak_arg2 () in
      let* the_fsm_b = build_fsm ~weak:weak2 blts bx refs in
      result_log (module Model.FSM) (module Decode.FSM)
      |> handle_results Info "Finished Making FSM B" the_fsm_b;
      M.return (the_fsm_a, the_fsm_b)
    ;;

    let do_merge { a; b } refs : Model.Bisimilarity.t option M.mm =
      Logger.trace __FUNCTION__;
      let open M.Syntax in
      let* the_fsm_a, the_fsm_b = build_fsms a b refs in
      Logger.info "Merging FSMs...";
      let the_fsm = FSM.merge the_fsm_a the_fsm_b in
      result_log (module Model.FSM) (module Decode.FSM)
      |> handle_results Result "Finished Merging FSMs" the_fsm;
      M.return None
    ;;

    let fail_if_not_bisim (x : Model.Bisimilarity.Result.t) : unit =
      if !Api.the_fail_flags.non_bisimilar
      then
        if Bool.not (Model.Bisimilarity.Result.are_bisimilar x)
        then (
          result_log (module Model.Bisimilarity.Result) (module Decode.Result)
          |> handle_results Result "Not Bisimilar" x;
          M.Err.not_bisimilar ())
    ;;

    let do_check_bisim { a; b } refs : Model.Bisimilarity.t option M.mm =
      Logger.trace __FUNCTION__;
      let open M.Syntax in
      let* the_fsm_a, the_fsm_b = build_fsms a b refs in
      check_saturation_size "FSM A" the_fsm_a;
      check_saturation_size "FSM B" the_fsm_b;
      Logger.info "Checking Bisimilarity of FSMs...";
      let result = Model.Bisimilarity.fsm the_fsm_a the_fsm_b in
      let r = result_log (module Model.FSM) (module Decode.FSM) in
      r |> handle_results Result "FSM a (original)" result.fsm_a.original;
      r |> handle_results Result "FSM a (saturated)" result.fsm_a.saturated;
      r |> handle_results Result "FSM b (original)" result.fsm_b.original;
      r |> handle_results Result "FSM b (saturated)" result.fsm_b.saturated;
      result_log
        ~decode:false
        (module Model.Bisimilarity.Result)
        (module Decode.Result)
      |> handle_results Result "Finished Merging FSMs" result.result;
      fail_if_not_bisim result.result;
      M.return (Some result)
    ;;

    exception NothingToBenchmark

    let _log_kinds ?(__FUNCTION__ : string = "") (x : EConstr.t) : unit M.mm =
      M.state (fun env sigma ->
        Rocq_utils.list_of_econstr_kinds sigma x
        |> List.iter (fun (s, b) ->
          Logger.debug ~__FUNCTION__ (Printf.sprintf "%b : %s" b s));
        sigma, ())
    ;;

    let rec extract_benchmark_args (xs : EConstr.t) : EConstr.t list M.mm =
      Logger.debug __FUNCTION__;
      let open M.Syntax in
      let* ty = M.type_of_econstr xs in
      let* kxs = M.econstr_kind xs in
      let* kty = M.econstr_kind ty in
      match kty, kxs with
      | App (ty, _), App (_, tys) ->
        let* is_list : bool = Theory.is_list ty in
        if is_list
        then
          if Int.equal (Array.length tys) 1
          then M.return []
          else
            let* tl = extract_benchmark_args tys.(2) in
            M.return (tys.(1) :: tl)
        else raise NothingToBenchmark
      | _, _ ->
        (* NOTE: isn't a list, so treat as single lts *)
        M.return [ xs ]
    ;;

    type graph_benchmark =
      string * (Constrexpr.constr_expr -> LTS.t) * Constrexpr.constr_expr

    let do_benchmark_graph
          (((xs, primary_lts), (time, repeat)) : rocq_args * (int * int))
          refs
      : Model.Bisimilarity.t option M.mm
      =
      Logger.trace __FUNCTION__;
      let open M.Syntax in
      let* xs : EConstr.t = M.constrexpr_to_econstr xs in
      let* xs : EConstr.t list = extract_benchmark_args xs in
      let f (i : int) (funs : graph_benchmark list) : graph_benchmark list M.mm =
        Logger.debug __FUNCTION__;
        let test_name : string = Printf.sprintf "benchmark_graph_%i" i in
        let* x : Constrexpr.constr_expr =
          M.state (fun env sigma ->
            sigma, List.nth xs i |> Rocq_utils.econstr_to_constrexpr env sigma)
        in
        let runf = fun x -> M.run (build_lts primary_lts x refs) in
        (test_name, runf, x) :: funs |> M.return
      in
      let* funs : graph_benchmark list =
        M.iterate 0 (List.length xs - 1) [] f
      in
      let samples = Benchmark.throughputN ~style:All ~repeat time funs in
      handle_results Result "benchmark lts graph" samples (module Benchmarking);
      M.return None
    ;;

    let run (refs : Libnames.qualid list) (x : t)
      : Model.Bisimilarity.t option M.mm
      =
      Logger.trace __FUNCTION__;
      let open M.Syntax in
      Config.load_the_bounds_args ();
      let* () = Config.load_weak_args () in
      match x with
      | MakeLTS args -> do_make_lts args refs
      | MakeFSM args -> do_make_fsm args refs
      | Saturate args -> do_saturate args refs
      | Minimize args -> do_minimize args refs
      | Merge args -> do_merge args refs
      | CheckBisim args -> do_check_bisim args refs
      | BenchmarkGraph args -> do_benchmark_graph args refs
    ;;
  end
end

(** [make ?enc ?ctx] constructs a [Wrapper.S] module.
    @param ?enc
      is a function returning a [module Encoding.S]. The default encoding uses [Int.t].
    @param ?ctx is the rocq-context.

    There is no longer a [?log]: output goes through [Logger] against the sink
    installed at plugin load, so a wrapper no longer carries a logger. *)
let make ?(enc : unit -> (module Encoding.S) = Api.make_enc_int) () : (module S)
  =
  let module Enc : Encoding.S = (val enc ()) in
  (module Make (Enc) : S)
;;

(** The instance the [MeBi ...] vernaculars run against.

    Every command used to call [make ()] inside its own action block, rebuilding
    [Enc], [Model], [Decode], [Theory], [Weak] and [Config] -- and a fresh
    encoding table -- on each invocation. Since the Rocq context is no longer a
    functor parameter, nothing about a command depends on the instance being
    fresh, so there is one.

    Per-command state is carried by the [~reset_encoding:true] every call site
    already passes: [Rocq_monad.run] then calls [Bi_encoding.reset], which
    clears the maps and resets [Enc.counter]. That flag was previously inert,
    because a fresh [Bi_encoding] has [the_maps = None] and so reset regardless.
    Bounds and weak-mode config are reloaded by [Command.run] itself. *)
let the_wrapper : (module S) option ref = ref None

(* Lazy rather than initialised at load: constructing it runs Api.make_enc_int,
   which should not race the sink install in Rocq_output. *)
let get () : (module S) =
  match !the_wrapper with
  | Some w -> w
  | None ->
    let w : (module S) = make () in
    the_wrapper := Some w;
    w
;;

let reset () : unit = the_wrapper := None
