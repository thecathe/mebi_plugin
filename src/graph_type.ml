module type S = sig
  type enc
  type tree
  type ind
  type action
  type weak
  type 'a mm

  module States : Set.S with type elt = enc
  module Destinations : Set.S with type elt = enc * tree

  module Actions : sig
    include Hashtbl.S with type key = action

    type t' = Destinations.t t

    val size : t' -> int
    val update : t' -> key -> Destinations.t -> unit
  end

  module Transitions : sig
    include Hashtbl.S with type key = enc

    type t' = Actions.t' t

    val size : t' -> int
    val update : t' -> key -> action -> Destinations.t -> unit
  end

  module B : Hashtbl.S with type key = enc
  module F : Hashtbl.S with type key = EConstr.t

  type indmap = ind B.t

  type t =
    { to_visit : enc Queue.t
    ; init : enc
    ; states : States.t
    ; transitions : Transitions.t'
    ; ltsmap : indmap
    ; primarylts : ind
    ; weak : weak option
    ; bounds : Api.bounds_args
    }

  val create : enc -> indmap -> ind -> weak option -> t

  exception NoMoreToVisit

  val next_to_visit : t -> enc
  val update_to_visit : t -> enc -> unit
  val update_states : t -> States.t -> t
  val is_silent_label : Evd.econstr -> weak option -> bool option mm
end

module type Args = sig
  type enc
  type tree

  module S : Set.S with type elt = enc
  module D : Set.S with type elt = enc * tree
  module T : Hashtbl.S with type key = enc

  val bounds : Api.bounds_args
end

module Make
    (Enc : Encoding.S)
    (M : Rocq_monad_utils.S with type enc = Enc.t and type tree = Enc.Tree.t)
    (Weak : Weak.S with type enc = Enc.t)
    (Theory :
       Theories_enc.S
       with type enc = Enc.t
        and type 'a mm = 'a M.mm
        and type 'a im = 'a M.mm)
    (ConstructorBindings :
       Constructor_bindings.S with type 'a mm = 'a M.mm and type ind = M.Ind.t)
    (Model :
       Model.S
       with type base = Enc.t
        and type tree = Enc.Tree.t
        and type trees = Enc.Trees.t
        and type constructorbindings = ConstructorBindings.t)
    (X : Args with type enc = Enc.t and type tree = Enc.Tree.t) :
  S
  with type enc = Enc.t
   and type tree = Enc.Tree.t
   and type ind = M.Ind.t
   and type action = Model.Action.t
   and type weak = Weak.t
   and module B = M.B
   and module F = M.F
   and type 'a mm = 'a M.mm = struct
  module B = M.B
  module F = M.F

  type enc = Enc.t
  type tree = Enc.Tree.t
  type ind = M.Ind.t
  type action = Model.Action.t
  type weak = Weak.t
  type 'a mm = 'a M.mm

  (** The model's actions, which label the graph's steps. *)
  module Action = Model.Action

  (* See the [.mli]. *)
  module States : Set.S with type elt = Enc.t = X.S

  (* See the [.mli]. *)
  module Destinations : Set.S with type elt = Enc.t * Enc.Tree.t = X.D

  (* See the [.mli]. *)
  module Actions = struct
    module Map_ : Hashtbl.S with type key = Action.t = Hashtbl.Make (Action)
    include Map_

    type t' = Destinations.t t

    (* See the [.mli]. *)
    let size (xs : t') : int =
      Logger.trace __FUNCTION__;
      fold (fun k v n -> Destinations.cardinal v + n) xs 0
    ;;

    (* See the [.mli]. *)
    let update (x : t') (action : Action.t) (states : Destinations.t) : unit =
      Logger.trace __FUNCTION__;
      if Destinations.is_empty states
      then ()
      else (
        match find_opt x action with
        | None -> add x action states
        | Some old_states ->
          replace x action (Destinations.union old_states states))
    ;;
  end

  (* See the [.mli]. *)
  module Transitions = struct
    module Map_ : Hashtbl.S with type key = Enc.t = X.T
    include Map_

    type t' = Actions.t' t

    (* See the [.mli]. *)
    let size (xs : t') : int =
      Logger.trace __FUNCTION__;
      fold (fun k v n -> Actions.size v + n) xs 0
    ;;

    (* See the [.mli]. *)
    let update
          (x : t')
          (from : Enc.t)
          (action : Action.t)
          (destinations : Destinations.t)
      : unit
      =
      Logger.trace __FUNCTION__;
      match find_opt x from with
      | None ->
        [ action, destinations ] |> List.to_seq |> Actions.of_seq |> add x from
      | Some actions -> Actions.update actions action destinations
    ;;
  end

  (* See the [.mli]. *)
  type indmap = M.Ind.t M.B.t

  (* See the [.mli]. *)
  type t =
    { to_visit : Enc.t Queue.t
    ; init : Enc.t
    ; states : States.t
    ; transitions : Transitions.t'
    ; ltsmap : indmap
    ; primarylts : M.Ind.t
    ; weak : Weak.t option
    ; bounds : Api.bounds_args
    }

  (* See the [.mli]. *)
  let create
        (init : Enc.t)
        (ltsmap : indmap)
        (primarylts : M.Ind.t)
        (weak : Weak.t option)
    : t
    =
    { to_visit = Queue.create ()
    ; init
    ; states = States.empty
    ; transitions = Transitions.create 0
    ; ltsmap
    ; primarylts
    ; weak
    ; bounds = X.bounds
    }
  ;;

  exception NoMoreToVisit

  (* See the [.mli]. *)
  let next_to_visit (g : t) : Enc.t =
    Logger.trace __FUNCTION__;
    try Queue.take g.to_visit with Queue.Empty -> raise NoMoreToVisit
  ;;

  (* See the [.mli]. *)
  let update_to_visit (g : t) (x : Enc.t) : unit =
    Logger.trace __FUNCTION__;
    Queue.add x g.to_visit
  ;;

  (* See the [.mli]. *)
  let update_states (g : t) (xs : States.t) : t =
    Logger.trace __FUNCTION__;
    { g with states = States.union g.states xs }
  ;;

  (* See the [.mli]. *)
  let is_silent_label (x : EConstr.t) : Weak.t option -> bool option M.mm =
    Logger.trace __FUNCTION__;
    function
    | None -> M.return None
    | Some (Option label_enc) ->
      Enc.log ~__FUNCTION__ ~m:Trace ~s:"Option" label_enc;
      let open M.Syntax in
      let* b : bool = Theory.is_None x in
      Logger.trace ~__FUNCTION__ (Printf.sprintf "%b" b);
      M.return (Some b)
    | Some (Custom (tau_enc, label_enc)) ->
      Enc.log ~__FUNCTION__ ~m:Trace ~s:"Custom" tau_enc;
      let act_enc : Enc.t = M.encode x in
      let b : bool = Enc.equal tau_enc act_enc in
      M.return (Some b)
  ;;
end
