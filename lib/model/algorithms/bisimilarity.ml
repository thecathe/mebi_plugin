module type S = sig
  type states
  type partition
  type fsm

  module FSMPair : sig
    type t =
      { original : fsm
      ; saturated : fsm
      }

    include Json.S with type k = t

    val get : fsm -> t
  end

  module Result : sig
    type t =
      { bisim_states : partition
      ; non_bisim_states : partition
      ; roots_related : bool option
      }

    include Json.S with type k = t

    val are_bisimilar : t -> bool
    val split : ?roots_related:bool -> partition -> states -> states -> t
  end

  type t =
    { fsm_a : FSMPair.t
    ; fsm_b : FSMPair.t
    ; merged : fsm
    ; result : Result.t
    }

  include Json.S with type k = t

  val the_cached_result : t option ref
  val set_the_result : t -> unit

  exception NoCachedResult of unit

  val get_the_result : unit -> t
  val fsm : fsm -> fsm -> t
end

module Make
    (C : Components.S)
    (FSM :
       FSM.S
       with type state = C.State.t
        and type states = C.State.Set.t
        and type labels = C.Label.Set.t
        and type edgemap = C.EdgeMap.t'
        and type info = C.Info.t)
    (Minimization :
       Minimization.S
       with type state = C.State.t
        and type states = C.State.Set.t
        and type label = C.Label.t
        and type labels = C.Label.Set.t
        and type edgemap = C.EdgeMap.t'
        and type partition = C.Partition.t
        and type fsm = FSM.t) :
  S
  with type states = C.State.Set.t
   and type partition = C.Partition.t
   and type fsm = FSM.t = struct
  module States = C.State.Set
  module Partition = C.Partition

  type states = States.t
  type partition = Partition.t
  type fsm = FSM.t

  module FSMPair = struct
    type t =
      { original : FSM.t
      ; saturated : FSM.t
      }

    include Json.Thing.Make (struct
        type k = t

        let name = "FSM Pair"

        let json ?as_elt (x : t) : Yojson.t =
          `Assoc
            [ "original", FSM.json ~as_elt:true x.original
            ; "saturated", FSM.json ~as_elt:true x.saturated
            ]
        ;;
      end)

    let get (x : FSM.t) : t =
      { original = x; saturated = FSM.saturate ~only_if_weak:true x }
    ;;
  end

  module Result = struct
    type t =
      { bisim_states : Partition.t
      ; non_bisim_states : Partition.t
      ; roots_related : bool option
      }

    include Json.Thing.Make (struct
        type k = t

        let name = "Result"

        let json ?as_elt (x : t) : Yojson.t =
          `Assoc
            [ "bisimilar states", Partition.json ~as_elt:true x.bisim_states
            ; ( "non-bisimilar states"
              , Partition.json ~as_elt:true x.non_bisim_states )
            ; ( "initial states related"
              , match x.roots_related with
                | None -> `Null
                | Some b -> `Bool b )
            ]
        ;;
      end)

    (* Bisimilarity of two systems is bisimilarity of their initial states.
       "Every block holds states of both systems" is not it: [a.b.x] against
       [b.a.y] partitions into two shared blocks, {x, b.y} and {b.x, y}, with
       the two initial states in different ones. It survives only as the
       fallback when an FSM has no initial state. *)
    let are_bisimilar ({ non_bisim_states; roots_related; _ } : t) : bool =
      Logger.trace __FUNCTION__;
      match roots_related with
      | Some related -> related
      | None -> Partition.is_empty non_bisim_states
    ;;

    let split
          ?(roots_related : bool option)
          (pi : Partition.t)
          (a : States.t)
          (b : States.t)
      : t
      =
      Logger.trace __FUNCTION__;
      let bisim_states, non_bisim_states =
        Partition.fold
          (fun (x : States.t) (bisim_states, non_bisim_states) ->
            if States.has_shared_origin x a b
            then Partition.add x bisim_states, non_bisim_states
            else bisim_states, Partition.add x non_bisim_states)
          pi
          (Partition.empty, Partition.empty)
      in
      { bisim_states; non_bisim_states; roots_related }
    ;;
  end

  type t =
    { fsm_a : FSMPair.t
    ; fsm_b : FSMPair.t
    ; merged : FSM.t
    ; result : Result.t
    }

  include Json.Thing.Make (struct
      type k = t

      let name = "Bisimilarity Results"

      let json ?as_elt (x : t) : Yojson.t =
        `Assoc
          [ ( "fsms"
            , `Assoc
                [ "a", FSMPair.json ~as_elt:true x.fsm_a
                ; "b", FSMPair.json ~as_elt:true x.fsm_b
                ; "merged", FSM.json ~as_elt:true x.merged
                ] )
          ; "result", Result.json ~as_elt:true x.result
          ]
      ;;
    end)

  let the_cached_result : t option ref = ref None
  let set_the_result (x : t) : unit = the_cached_result := Some x

  exception NoCachedResult of unit

  let get_the_result () : t =
    Logger.trace __FUNCTION__;
    match !the_cached_result with
    | None -> raise (NoCachedResult ())
    | Some x -> x
  ;;

  let fsm (a : FSM.t) (b : FSM.t) : t =
    Logger.trace __FUNCTION__;
    let fsm_a : FSMPair.t = FSMPair.get a in
    let fsm_b : FSMPair.t = FSMPair.get b in
    let merged : FSM.t = FSM.merge fsm_a.saturated fsm_b.saturated in
    (* [merged] is already saturated, so partition it as it is.
       [Minimization.fsm] would saturate it again -- a pass that rebuilds
       every weak action of both FSMs (the merged FSM has no silent edges
       left, so it reproduces the same structure, and the partition only
       reads that), roughly doubling the check's saturation memory. *)
    let pi : Partition.t = Minimization.partition_states merged in
    let roots_related : bool option =
      match fsm_a.original.init, fsm_b.original.init with
      | Some x, Some y ->
        Some
          (States.mem
             y
             (try Partition.get_bisimilar x pi with Not_found -> States.empty))
      | _ -> None
    in
    let result =
      Result.split
        ?roots_related
        pi
        fsm_a.original.states
        fsm_b.original.states
    in
    { fsm_a; fsm_b; merged; result }
  ;;
end
